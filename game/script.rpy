# Игра начинается здесь:
label start:

    show screen translate

# Сцена 0. Обучение
label s1scene0:

    scene schoolyard1

    menu:
        "Native language, родной язык, Lengua materna, Lingua madre, Langue maternelle"

        "English":
            $ renpy.config.native_language = "english"  
        "Русский":
            $ renpy.config.native_language = "russian"            
        "Español":
            $ renpy.config.native_language = "spanish"

    $ preferences.language = renpy.config.native_language
    $ renpy.restart_interaction()

    show sam a_n at center
    with moveinright

    $ set_study_mode(False)

    s "Добрый вечер. Надеюсь вы справились с выбором родного языка, и сейчас вы понимаете, что я вам говорю."

    s "Я тут немного испачкался, попал в небольшую аварию. Но об этом позже."

    s "Вы в визуальной новелле 'Секрет королевской школы магии'. Но мы тут с вами будем не только магию изучать, но и иностранный язык."

    s "Выбери язык, который ты хочешь изучать."

    menu:
        s  "Какой язык ты хочешь изучать?"

        "English":
            $ renpy.config.study_language = "english"
        "Русский":
            $ renpy.config.study_language = "russian"
        "Español":
            $ renpy.config.study_language = "spanish"

    $ load_quiz_texts()  # Загружаем тексты квизов
    $ load_vocabulary() # загружаем словари
    $ load_all_karaoke_data()  #загружаем тексты песен

    s "Когда справа ты увидишь круглую кнопку, можешь нажать на нее, чтобы перевести реплику. Давай попробуем."

    $ set_study_mode(True)

    s "Жми на эту кнопку справа ———>"

    $ set_study_mode(False)

    s "Чтобы вернуть исходный текст, нажимай на эту кнопку повторно."

    s "Если ты что-то напутал с выбором языков, можешь исправить это в меню 'Опции'. А также там ты можешь отредактировать громкость музыки."

    s "Вроде все самое главное я рассказал. Где-то здесь начинается наша история. Точнее она уже началась. Погнали."

    $ set_study_mode(True)



# Сцена 1. Автоавария
label s1scene1:

    scene driving_scene

    show sam a_n at left

    show dad a_n at right

    $ show_vocabulary_screen("s1scene1")

    n "Радиодиктор: Король объявил о своей свадьбе с дочерью министра экономики."

    s a_smirk "Наш король снова собрался женится? Не поздно ли женится в 98 лет?"

    d "Он выглядит на 35."

    s a_sr "Разве это нестранно?"

    d "Многие высшие чиновники молодо выглядят и долго живут в наше время. У них совсем другие возможности."

    play sound "audio/boomcar.mp3"
    with hpunch
    with vpunch
    #"Бум." (тряска, черный экран)

    show black

    s "Папа, ты меня слышишь? Очнись!"

    hide black

    scene highway1

    $ set_study_mode(False)
    menu:
        "Пора пройти небольшой квиз." 
        "Пройти квиз":
            $ show_quiz_screen("quiz_s1scene1", s)
        "Пропустить квиз":
            pass


# Сцена 2. Виновник аварии
label s1scene2:

    $ set_study_mode(True)

    play music "audio/magic-soundtrack-anxiety.mp3" fadein 1.0

    scene highway1
    with Dissolve(1.0)
    
    show sam a_n at left
    with moveinleft

    show ds a_n at center
    with moveinright
    
    s a_evil "Вызывайте скорую, скорее! Мой отец без сознания."
    
    sm a_eb "Он выглядит странно. Почему он такой бледный? Тоже пострадал?"
    
    s a_magic "Блок!!! Он пытался стереть мою память? Что за черт!"    
    
    ds "Что это за магия? Где ты обучался?"
    
    s a_sr "Отдайте телефон. Я сам вызову скорую. Только попробуйте снова применить к нам магию!"
    
    ds "Не нужно скорую, я сам все улажу."
    
    "Он подошел к водительскому сиденью. Открыл дверь и коснулся головы моего отца. Отец тут же пришел в сознание. Но незнакомец пошатнулся, облокотился на дверь и сполз вниз на асфальт."
    
    s a_uuu"Что с вами? Вы не пьяны…"
    
    sm a_eb "Что-то с ним не так. Что-то не так с его магией. Его энергия нестабильная. Кто-то аккумулировал его энергию. Это всегда связано с запрещенными приемами. Он из секты?"
    
    "Незнакомец собрался с силами и встал."
    
    ds "Простите, я плохо себя чувствовал и потерял контроль. Вот деньги на ремонт и лечение, если оно понадобится."
    
    show dad a_n at right
    with moveinright
    
    d "Ого! Тут много денег! Но зачем вам самому платить деньги? У вас дорогая машина. Неужели вы не платите страховку?"
    d "И вы выглядите странно. Вы пьяны? Или наркотики? В любом случае нам нужно вызвать дорожную службу. Вам повезло, что мой сын не пострадал."
    
    ds "Не звоните. У меня к вам есть хорошее предложение."
    ds "Ваш сын явно очень талантлив. Но без должного образования он сможет играться с магией только для развлечения. И это только создаст для него проблемы."
    
    d "Причем здесь мой сын? К чему вы клоните?"
    
    ds "Я могу устроить вашего сына в королевскую школу магии."
    
    d "Королевскую школу? Это невозможно. Дети из простых семей не попадают в эту школу."
    
    ds "Я могу это сделать."
    
    d "Кто вы?"
    
    ds "Я устрою вашего сына в эту школу, а взамен, вы скажете всем, что врезались в столб. Даже близким людям."
    extend " Я скажу, кто я, если вы согласны на мое условие."
    
    s a_sr "Папа, с чего нам ему верить? Такие, как он, всегда все исправляют деньгами и связями. Давай не будем у него ничего брать? Прошу тебя. У меня плохое предчувствие."
    
    ds "Молодой человек, а вы, я смотрю, дерзкий. Но я советую вам всегда брать все, что вам дают. Особенно сегодня. Сегодня судьба на вашей стороне."
    
    s a_eb "Судьба на нашей стороне, потому что мы несильно пострадали."
    
    d "Предоставьте гарантии."

    s a_sr "Папа!"
    
    ds "Ладно, вот моя визитка."
    
    d "Директор королевской школы магии... Серьезно?"
    
    s a_sr "Это какой-то новый вид мошенничества?"
    
    d "Ладно, мы подумаем."

    $ ds_name = "Директор школы"
    
    ds "Позвоните мне на номер с визитки. Списки учеников будут сформированы через три дня. Вам стоит поторопиться."


# Сцена 3. Размышления на кухне
label s1scene3:

    scene kitchen
    with Dissolve(1.0)

    show sam a_n at left
    with moveinleft

    show dad a_n at right
    with moveinright

    s "Я погуглил. Это и правда был директор школы. Но папа! Он пытался мне стереть память. Это запрещенная магия!"
    s "Давай лучше не связываться с этим человеком. Ему ничего не стоит нас подставить."
    s a_sr "Вряд ли он так испугался из-за обычной аварии. Думаю, он боялся, что вскроется что-то похуже."

    d "Что ты имеешь ввиду?"

    s "Может в тот день он совершил преступление с черной магией? С его энергией что-то было не так."

    d "Не придумывай! Ты просто очень молод, и у тебя богатая фантазия. Зачем директору школы магии использовать запрещенную магию."

    s a_n "Кто знает…"

    d "Прекрати это. Это такой шанс. Эта школа открывает двери совсем в другой мир. Ты сможешь занимать высокие посты, устроится в любую компанию мира. Твой талант!"

    s a_sr "Я обучался магии не для того, чтобы строить карьеру."

    d "Каждый день выходят новые законы, накладывающие запреты на магию для людей без лицензии. Тебе уже 14, любая неосторожность, и ты можешь попасть в тюрьму."
    d "Я столько раз успел пожалеть, что позволил твоей бабушке обучать тебя. Но после учебы в этой школе ты сможешь использовать свои способности в полной мере."

    scene black with Dissolve(2.0)  # Сначала затемнение


label s1karaoke1:

    scene schoolyard1
    with Dissolve(1.0)

    menu:
        "Слушать караоке":
            play karaoke "song-king-take-magic.mp3" 
            $ show_karaoke("song_1")
            stop karaoke fadeout 1.0
        "Пропустить":
            pass  # Пропустить караоке

    scene black with Dissolve(2.0)  # Сначала затемнение


# Сцена 4. Школьная форма
label s1scene4:

    play music "audio/magic-soundtrack-main.mp3" fadein 1.0

    scene samroom
    with Dissolve(2.0)

    show sam a_n at left
    with dissolve

    show dad a_n at right
    with dissolve

    s a_smirk "Я нашел б/у школьную форму."

    d "Богачи тоже продают б/у вещи?"

    s a_xm "Сомневаюсь. Думаю, они их просто выкидывают, а гувернантки их продают. Вместо чаевых."

    d "О, наверное, ты прав. Но я могу купить тебе новую форму."

    s a_n "Не нужно. Мне не нужны все эти понты. Все всё-равно быстро узнают, что я из обычной семьи. Я заказываю."

    d "Наверное, ты переживаешь об этом. Они не смогут тобой помыкать. Ты справишься. Тебе нужно продержаться всего 4 года."

    s a_n "Я сделаю это, потому что ты так хочешь этого."

    scene black with Dissolve(3.0)  # Сначала затемнение


#Сцена 5 Первый день
label s1scene5:

    scene schoolyard1
    with Dissolve(1.0)

    show sam sch a_n at right3
    with moveinright

    s sch a_xm "Двери школы закрыты магией? Значит даже уборщицы в этой школе владеют основами магии? Но это простой для меня замок…"

    show schoolgirl1 a_n at left2
    with moveinleft

    schoolgirl1 "Смотрите, новенький!"

    show schoolgirl2 a_n at left1
    with moveinleft

    schoolgirl2 "Его форма явно неновая. Сегодня же его первый день…"

    schoolgirl1 "Он купил б/у форму? Фу, какой кошмар."

    schoolgirl2 "А его обувь! Посмотрите на нее! Что это? Какая-то дешевая реплика? Школа начала принимать всех подряд?"

    sm sch a_eb "Они могли бы говорить это хотя бы шепотом. Разве в королевской школе не учат хорошим манерам? Да, хорошее начало…"
    extend sch a_sr " Но я не ожидал ничего другого от детей продажных чиновников."

    show sam sch a_n at center
    with moveinright
    
    show trainee a_n at right
    with moveinright

    trainee "Привет, ты Сэм?"

    hide schoolgirl2
    with moveoutleft

    hide schoolgirl1
    with moveoutleft

    s "Да, это я."

    trainee "Я пришла тебе встретить. Я прохожу здесь практику. Пойдем, я выдам тебе все необходимое. Волнуешься?"

    s sch a_eb"А должен?"

    trainee "Дети в этой школе непростые. Но их жизнь не такая легкая, как многие думают. Когда ты узнаешь их лучше, они тебе понравятся."

    s sch a_sr "Очень сомневаюсь."

    menu:
        "Поговорить с ней еще":
            s sch a_n "Здесь сложно учиться?"

            trainee "Думаю, тебе будет несложно. Я слышала, у тебя хорошие навыки магии."

            s sch a_smirk "Что? Я уже так популярен?"

            trainee "Ха-ха. Не каждый день к нам берут неизвестных новичков."
            trainee "Обо всех учениках в этой школе можно прочитать в интернете. Кто их родители, какие бренды они носят, чем занимались в эти выходные."
            extend " А твое появление покрыто завесой тайны."

            s sch a_tt "Я тайный внебрачный сын директора."

            trainee "Что??? Правда?"

            s sch a_smirk "Нет, конечно."

            sm sch a_tt "А она милая. И симпатичная. Может в этой школе будет и что-то хорошее."

            s sch a_n "А что насчет вас? Я слышал, что все учителя в этой школе выпускники элитных школ и университетов. Так кто ваши родители?"

            trainee "Мои родители обычные люди… Но я действительно училась в этой школе."

            s sch a_eb "Так я не первый неэлитный ученик в этой школе?"

            trainee "А ты забавный! "
            extend " Я выиграла грант в честь девяностолетия короля. А ты как все же попал сюда? Это тайна?"

            s sch a_n "Я расскажу эту тайну только вам."
            extend sch a_smirk" Мой невероятный талант и безграничная харизма растопили холодное сердце директора, и он сделал для меня особое исключение."

            trainee "Не похоже на правду!"

            s sch a_smirk "Возможно, молодые симпатичные мальчики в его вкусе."

            trainee "Фу! А вот так лучше тебе не шутить."

        "Промолчать":
            pass

    trainee "Мы пришли. Вот твоя школьная карточка. Ты можешь пользоваться ей в столовой."
    extend " Возьми в библиотеке учебники после первого урока. А вот список учебников и материалов, которые нужно купить."

    s sch a_uuu "Ух. Надеюсь, нам с папой не придется брать кредит."

    trainee "Удачи тебе, таинственный новичок. Твой первый урок в кабинете 313."

#Сцена 6. Первый урок
label s1scene6:

    scene classroom
    with Dissolve(1.0)

    show teacher a_n at left

    show sam sch a_n at center
    with moveinleft

    teacher "В нашем классе новый ученик. Представься."

    sm sch a_uuu "Они развязывают мои шнурки, серьезно? Что за ребячество!"
    extend sch a_tt " Ооо. Кто-то расстегивает мне ширинку? Ха! Недурно. Надеюсь, это делает девушка. Да, это точно девушка. Какой нежный телекинез. Это точно буллинг?"

    n "Класс: Ха,ха,ха!!!"

    teacher "Что вы творите? Прекратите!"

    s sch a_sr "Не думаю, что кто-то здесь станет утруждаться запоминать мое имя. "

    sm sch a_eb "Хм. И что это касается моей спины? Пишут что-то у меня на спине мелом? "
    extend sch a_magic" Ладно, хватит игр! Сосредоточиться! Раз, два, три… Больше концентрации. Собрать в пальцах. Та-дам."

    n "Все ученики в мгновение оказались привязаны к стульям и столам за все те шнурки и веревочки, которые только были на их одежде."

    teacher "…."
    

    show student1 a_n at right3
    with moveinright

    student1 "Что за хрень?"

    hide student1
    with moveoutright

    show president_son a_n at right3
    with moveinright

    ps "Его перевели из другой школы магии? Разве он не из отбросов?"

    hide president_son
    with moveoutright

    show schoolgirl1 a_n at right3
    with moveinright

    schoolgirl1 "Ему это не сойдет с рук!"

    hide schoolgirl1

    teacher "Сэмюэл! Вам еще не рассказали, что магию в этой школе нельзя использовать для баловства?"

    s sch a_smirk "Да, мне это рассказали. Но, видимо, мне забыли рассказать, что я единственный в этой школе, кому запрещено баловаться магией."

    n "Класс: Ха…"

    show schoolgirl1 a_n at right3
    with moveinright

    schoolgirl1 "А он прикольный!"

    hide schoolgirl1
    with moveoutright

    s sch a_n "Что ж. Вы сейчас все думаете, о том, что я не один из вас. И что я никогда не стану одним из вас. Так вот, знайте - я и не собираюсь. И я не надеюсь, что мы подружимся. Давайте просто не будем донимать друг друга."

    show student1 a_n at right3
    with moveinright

    student1 "Кем он себя возомнил! Я поставлю его на место."

    show president_son a_n at right2
    with moveinright

    ps "(шепотом) Сегодня после уроков проучите его. А я угощу вас хорошим стейком."

    student1 "Хорошо. Я соберу парней. Заткнем этого выскочку. Стейк же говяжий?"

    hide president_son
    with moveoutright

    hide student1
    with moveoutright

#Сцена 7. Драка
label s1scene7:

    scene schoolyard1
    with Dissolve(1.0)

    show sam sch a_n at left
    with moveinleft

    sm sch a_n "Каким бы дерьмом не были люди в этой школе, уроки здесь действительно другого уровня. Было непросто…"

    play music "audio/magic-soundtrack-naptyajenie.mp3" fadein(1.0)

    sm sch a_eb"О, меня уже ждут. Не ожидал, что стану им так интересен в первый же день. "

    show student1 a_n at right2
    with moveinright

    show president_son a_n at right3
    with moveinright

    with hpunch
    with vpunch
    with hpunch
    n "Сэма сильно откинуло на несколько метров сильным энергетическим ударом. Он ударился спиной о школьный забор. Магическая защита забора резко обожгла спину."

    sm sch a_sr "Черт! Это было неожиданно!"

    s sch a_smirk "Разве плохие парни перед дракой не должны сначала произнести длинную торжественную речь?"  

    ps "Вжарьте ему еще раз! Кажется, он с первого раза не понял."

    sm sch a_n "Это явно их предводитель. Знакомое лицо. Я точно видел его, когда читал о школе. "

    sm sch a_magic "Собрать всю энергию… Поднять, сконцентрировать, усилить. Взять силу камней. Есть ли рядом металл? Забор… Сойдет. Взять силу металла. Теперь в глаза… Стена… Готово. Главное не моргнуть."

    student1 "Три, два, один… Жарим!"

    with hpunch

    show sam sch a_n

    n "Парней ударило отраженной от энергетической стены ударом и раскидало в разные стороны. "

    student1 "Что это? Черная магия?"

    s "Вы настолько плохи, что даже черное от белого не отличаете? "

    sm sch a_eb "Вспомнил. Он же девятый сын президента, его четырнадцатый ребенок. "

    s sch a_smirk "Так вот как развлекаются дети президента. Отличный сюжет для новостей. "

    ps "Вы что, втроем не можете справится с этим отбросом? "

    sm sch a_magic "Вот сейчас точно не стоит моргать! "

    # удар, тряска, черный экран
    with hpunch
    with vpunch

    sm sch a_n "Он смог пробить стену… "

    s "Поздравляю! А ты не слабак. "

    sm "Его дружки ударили одновременно с ним? Это точно энергия нескольких людей… "

    ps "Что, испугался? Сегодня я добрый. Просто уходи из этой школы. И я забуду о твоем существовании. "

    s sch a_eb "Но знаешь, есть кое-что странное. Это была энергия трех разных людей. И это не твои дружки. "

    ps "…"

    s sch a_n "Как ты обрел эти энергии? "

    ps "Что за бред ты несешь? Возомнил, что умеешь различать базисы энергий? Думаешь, кто-то в это проверит? "

    s sch a_xm "А ты еще мне не поверил? Ладно. Давай, ударь меня еще раз. "

    menu:        
        "Не ставить блок":
            with vpunch
            $ block_used = False
            n "В этот раз Сэма ударило еще более сильной волной, он позволил его, но постарался смягчить удар."
            s "Спасибо! Теперь я успел разобраться. "
            s "Одна энергия молодого парня, добрая и чистая. "
            extend "Одна энергия женщины средних лет, она была в отчаянье, вся ее энергия состоит из слез... "
            extend "И еще энергия взрослого мужчины… Двое из них развивали свою энергию явно не по лицензированным методикам."

        "Поставить блок":
            with hpunch
            with vpunch
            with hpunch
            $ block_used = True
            sm "Я инстинктивно отразил атаку. Мне нужно было пропусти удар, чтобы прочувствовать его энергию. Но глупо просить еще раз ударить меня. Они решат, что я сам желаю стать грушей для битья."
            s "Это точно было несколько разных базисов."

    ps "Кто ты такой? Кто тебе рассказал об этом? Я лично знаю всех в этом мире, кто способен различать базисы энергии. Значит среди них завелась крыса… "

    s sch a_sr "Очевидно, что ты знаешь не всех. Лучше держись от меня подальше. Иначе я не стану молчать. "

    ps "О, и кому ты расскажешь? Своей мамочке? Хотя, кажется, я слышал, что у тебя ее нет. Ты не продержишься здесь и месяца. Я приложу все усилия, чтобы тебя выгнали как можно скорее. "

    s sch a_smirk "А ты явно много мной интересовался. Даже моими родителями… Думаю, что теперь я тебе интересен еще больше. "
    
    scene black with Dissolve(3.0)  # Сначала затемнение


# Сцена 8. Разговор о маме
label s1scene8:

    play music "audio/magic-soundtrack-dream-sad.mp3" fadein 1.5

    scene kitchen
    with Dissolve(2.0)

    show sam a_n at left
    with moveinleft

    show dad a_n at right
    with moveinright

    s "Пап, а кто-нибудь способен забрать базис энергии другого человека себе?"

    d "Ты думаешь, что твоя мама умерла, потому что ты забрал ее энергию? "
    d "Эх, ты взрослеешь и, наверное, ты думаешь, что я мог обманывать тебя, чтобы утешить маленького ребенка?"
    extend " Никто не может забрать чужую энергию себе."

    s "А кто-то может намеренно отдать свою энергию?"

    d "Это так же невозможно, как силой мысли остановить собственное сердце. Наша жизненная энергия — это часть нас. Никто не может самостоятельно избавиться от нее."
    d "Поверь, все произошло случайно. Твоя мама хотела своей энергией поддержать твои жизненные силы, но в этот момент ее сердце остановилось."

    s a_sr "Значит, если человека убить в тот момент, когда он применяет свою энергию, можно получить его энергию себе?"

    d "Какие ужасные вопросы ты задаешь."

    s a_n "И все же."

    d "Люди с двумя энергиями встречаются так редко, что многие считают их существование вымыслом."
    d "Я спрашивал у твоей бабушки, как такое могло произойти. Она считала, что энергия перешла к тебе из-за того, что ты только-только родился, и твоя собственная энергия еще не полностью отделилась от энергии мамы'."

    s a_eb "Но я же непервый в мире ребенок, чья мама умерла после родов."

    d "Непервый. Но твоя мама умела подкреплять чужую жизненную энергию, также как и твоя бабушка. Это очень редкий навык."
    d "Я думаю, что, возможно, во всем мире только они двое и обладали этим умением по-настоящему."
    d "Остальные просто мошенники. Поэтому вряд ли миру известны еще подобные случаи."

    s a_n "О моем случае мир тоже не знает."

    d "Это ради тебя. Я рассказывал тебе, как много доставали твою маму, пытаясь уговорить работать в команде сохранения жизни и здоровья короля."
    d "Точнее это сложно назвать работой. Они заставляют людей служить им, они угрожают им."
    d "Я уверен, тебе это не нужно."

    s a_uuu "Ей угрожали?"

    d "Я не так выразился. Они умеют манипулировать."

    s a_n "Значит, я должен всю жизнь скрывать свою двойную энергию?"

    d "Ты можешь решать сам. Ты не обязан скрывать. Но, если кто-нибудь узнает, у них точно будут к тебе вопросы."
    d "Но когда ты закончишь эту школу, ты сможешь получить лицензию. И тогда тебе больше никогда не придется оправдываться за свои навыки."

    d "И ты же знаешь, что я очень рад, что ты выжил? Я никогда не думал, что это ты виноват в смерти мамы."

    s "Знаю… Но я бы на твоем месте думал по-другому. Спасибо тебе."

    hide dad
    with moveoutright

    sm a_sr "Откуда у сына короля три базиса?"

    if not block_used:
        sm a_eb "И все три базиса явно не его. Свой он не использует. Он точно не добрый парень, ни девушка и ни мужчина средних лет. У него вообще есть свой базис?"
    else:
        sm a_eb "У него два чужих базиса. А это уже не похоже на мой случай."

    sm a_n "Хотя вместе их сила была достаточной, по отдельности это довольно слабые  энергии… Все это очень странно."


    stop music fadeout 1.5
    n "Продолжение следует…"
    n "Спасибо за внимание"

return